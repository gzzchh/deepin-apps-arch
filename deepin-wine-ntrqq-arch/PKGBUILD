# Maintainer: Codist <countstarlight@gmail.com>
# Maintainer: taotieren <admin@taotieren.com>

pkgname=deepin-wine-ntrqq
pkgver=9.4.8.27843
qq_installer=PCQQ2021
debpkgver=9.3.2deepin20
debpkgname="com.qq.im.deepin"
pkgrel=1
pkgdesc="Tencent QQ with NtrQQ on Deepin Wine5(${debpkgname}) For Archlinux"
arch=("x86_64")
url="https://im.qq.com/"
license=('custom')
depends=('p7zip' 'wine' 'wine-mono' 'wine-gecko' 'xorg-xwininfo' 'wqy-microhei' 'lib32-alsa-lib' 'lib32-alsa-plugins' 'lib32-libpulse' 'lib32-openal' 'lib32-mpg123' 'lib32-gnutls')
conflicts=('deepin-qq-im' 'deepin.com.qq.im')
install="deepin-wine-qq.install"
_mirror="https://cdn-package-store6.deepin.com"
source=("$_mirror/appstore/pool/appstore/c/${debpkgname}/${debpkgname}_${debpkgver}_i386.deb"
  "run.sh"
  "make-msimg32-native.patch"
  "https://common-1251511762.cos.ap-guangzhou.myqcloud.com/qq-9.4.8-ntr-7.1.2-mod.7z")
md5sums=('5fdc20e614d945bd2ba5251420872479'
  'fc65fd18be9a56942aaadcdf431a85f8'
  'cc054d4ad1755c7a52c9f82de6082992'
  'cf44f2090321e692368559800f9daf83')

build() {
  msg "Extracting DPKG package ..."
  mkdir -p "${srcdir}/dpkgdir"
  tar -xvf data.tar.xz -C "${srcdir}/dpkgdir"
  sed "s/\(Categories.*$\)/\1Network;/" -i "${srcdir}/dpkgdir/opt/apps/${debpkgname}/entries/applications/${debpkgname}.desktop"
  sed "s/run.sh\".*/run.sh\"/" -i "${srcdir}/dpkgdir/opt/apps/${debpkgname}/entries/applications/${debpkgname}.desktop"
  msg "Extracting Deepin Wine QQ archive ..."
  7z x -aoa "${srcdir}/dpkgdir/opt/apps/${debpkgname}/files/files.7z" -o"${srcdir}/deepinqqdir"
  msg "Cleaning up the original package directory ..."
  rm -r "${srcdir}/deepinqqdir/drive_c/Program Files/Tencent/QQ"
  #msg "Patching reg files ..."
  #patch -p1 -d "${srcdir}/deepinqqdir/" < "${srcdir}/reg.patch"
  msg "Creating font file link ..."
  ln -sf "/usr/share/fonts/wenquanyi/wqy-microhei/wqy-microhei.ttc" "${srcdir}/deepinqqdir/drive_c/windows/Fonts/wqy-microhei.ttc"
  # msg "Copying latest QQ installer to ${srcdir}/deepinqqdir/drive_c/Program Files/Tencent/ ..."
  # install -m644 "${srcdir}/${qq_installer}-${pkgver}.exe" "${srcdir}/deepinqqdir/drive_c/Program Files/Tencent/"
  #find -L "${srcdir}/deepinqqdir/dosdevices" -maxdepth 1 -type l -delete
  msg "释放 NtrQQ"
  7z x -aoa qq-9.4.8-ntr-7.1.2-mod.7z -o"${srcdir}/deepinqqdir/drive_c/Program Files/Tencent" "QQ/*"
  msg "注册表补丁 "
  patch -p1 "${srcdir}/deepinqqdir/user.reg" < "${srcdir}/make-msimg32-native.patch"
  # msg "清理 NtrQQ 多余的东西"
  # rm "${srcdir}/deepinqqdir/drive_c/Program Files/Tencent/QQ/VideoBeautyInstall.exe"
  # rm "${srcdir}/deepinqqdir/drive_c/Program Files/Tencent/QQ/VideoMsgInstall.exe"
  # rm "${srcdir}/deepinqqdir/drive_c/Program Files/Tencent/QQ/VideoShowPlayerInstall.exe"
  msg "Repackaging app archive ..."
  7z a -t7z -r "${srcdir}/files.7z" "${srcdir}/deepinqqdir/*"
}

package() {
  msg "Preparing icons ..."
  install -d "${pkgdir}/usr/share/applications"
  install -Dm644 "${srcdir}/dpkgdir/opt/apps/${debpkgname}/entries/applications/${debpkgname}.desktop" "${pkgdir}/usr/share/applications/${debpkgname}.desktop"
  cp -r "${srcdir}/dpkgdir/opt/apps/${debpkgname}/entries/icons/" "${pkgdir}/usr/share/"
  msg "Copying deepin files ..."
  install -d "${pkgdir}/opt/apps/${debpkgname}/files"
  install -m644 "${srcdir}/files.7z" "${pkgdir}/opt/apps/${debpkgname}/files/"
  cp ${srcdir}/dpkgdir/opt/apps/${debpkgname}/files/helper_archive* "${pkgdir}/opt/apps/${debpkgname}/files/"
  #install -m755 "${srcdir}/dpkgdir/opt/apps/${debpkgname}/files/gtkGetFileNameDlg" "${pkgdir}/opt/apps/${debpkgname}/files/"
  md5sum "${srcdir}/files.7z" | awk '{ print $1 }' > "${pkgdir}/opt/apps/${debpkgname}/files/files.md5sum"
  #install -m644 "${srcdir}/reg.patch" "${pkgdir}/opt/apps/${debpkgname}/files/"
  install -m755 "${srcdir}/run.sh" "${pkgdir}/opt/apps/${debpkgname}/files/"
}
